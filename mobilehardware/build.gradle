import java.text.SimpleDateFormat

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "2.4.1"
apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    resourcePrefix "bounceprogressbar"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 241
        versionName "2.4.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            //改为不混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
def SDK_NAME = "mobile_hardware_android_v"
def DATE = new SimpleDateFormat('yyyyMMdd_HHmm').format(new Date())
def LIB_DIR = '../libs'
def version = "2.4.1"

task clearJar(type: Delete) {
    //删除旧包
    // println '删除旧包'
    // delete 'build/libs/'
    // delete '../libs/'
}

task makeJar(type: org.gradle.api.tasks.bundling.Jar) {
    //jar 包名称
    archiveName = SDK_NAME + version + '_' + DATE + '.jar'
    //指定jar 存放目录
    destinationDir = file(LIB_DIR)
    //从哪里打包class文件
    from('build/intermediates/classes/release/com/mobile/mobilehardware/')
    //打包到jar后的目录结构
    into('com/mobile/mobilehardware/')
    //去掉不需要打包的目录和文件
    exclude('test/', 'BuildConfig.class', 'R.class')
}
makeJar.dependsOn(clearJar, build)

//./gradlew makeJar


def siteUrl = 'https://github.com/guxiaonian/MobileInfo'      // 项目的主页
def gitUrl = 'https://github.com/guxiaonian/MobileInfo.git'   // Git仓库的url
group = "fairy.easy"                                        // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                // Add your description here
                name 'Android'    //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mobile'        //填写的一些基本信息
                        name 'mobile'
                        email 'gunaonian@geetest.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// bintrayUpload使用
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user =  properties.getProperty("bintray.user")
    key =  properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "mobile"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}